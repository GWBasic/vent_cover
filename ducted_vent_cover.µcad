use std::math::*;
use std::geo2d::*;
use std::ops::*;

const COVER_WIDTH = 291mm;
const COVER_HEIGHT = 138mm;
const COVER_DEPTH = 3mm;

const TOP_DUCT_DEPTH = 200mm;
const TOP_DUCT_ANGLE = 30deg;

const HOLE_WIDTH = 238mm;
const HOLE_HEIGHT = 88mm;

const NUM_SLATS = 30;
const FRACTION_OPEN = 1.0 / 2.0;

const DUCT_WIDTH = 246mm;
const DUCT_HEIGHT = 97mm;
const DUCT_DEPTH = 45mm;

// microcad does not yet have a built-in sqrt(3) function
const SQRT3 = 1.73205081;

part TopDuct() {
    angled_duct = {
            Rect(width = DUCT_HEIGHT, height = 0mm);
            Rect(width = 0mm, height = DUCT_HEIGHT)
                .translate(y = DUCT_HEIGHT / 2, x = DUCT_HEIGHT / 2)
                .translate(y = TOP_DUCT_DEPTH - DUCT_HEIGHT, x = TOP_DUCT_DEPTH / SQRT3);
        }
        .hull()
        .extrude(DUCT_WIDTH)
        .rotate(x = 90deg)
        .rotate(z = 90deg)
        .translate(x = -DUCT_WIDTH / 2);

    bottom_chop = {
            Rect(width = HOLE_HEIGHT, height = 0mm);
            Rect(width = 0mm, height = HOLE_HEIGHT)
                .translate(y = DUCT_HEIGHT / 2, x = DUCT_HEIGHT / 2)
                .translate(y = TOP_DUCT_DEPTH - DUCT_HEIGHT, x = TOP_DUCT_DEPTH / SQRT3);
        }
        .hull()
        .extrude(HOLE_WIDTH)
        .rotate(x = 90deg)
        .rotate(z = 90deg)
        .translate(x = -HOLE_WIDTH / 2);

    angled_duct - bottom_chop;
}

part Cover() {
    top = Rect(width = DUCT_WIDTH, height = DUCT_HEIGHT);

    slat_hole_width = (HOLE_WIDTH / (NUM_SLATS + 1)) * FRACTION_OPEN;
    slat_hole = Rect(width = slat_hole_width, height = HOLE_HEIGHT);

    holes = slat_hole.translate(x = ([0..NUM_SLATS] * (HOLE_WIDTH / NUM_SLATS)) - (HOLE_WIDTH / 2));

    (top - holes)
        .extrude(COVER_DEPTH)
        .rotate(x = 90deg)
        .translate(z = DUCT_HEIGHT / 2, y = DUCT_HEIGHT / 2)
        .translate(z = TOP_DUCT_DEPTH - DUCT_HEIGHT, y = TOP_DUCT_DEPTH / SQRT3 + COVER_DEPTH)
        ;
}

part Bottom() {
    duct = Rect(width = DUCT_WIDTH, height = DUCT_HEIGHT);
    duct_hole = Rect(width = HOLE_WIDTH, height = HOLE_HEIGHT);

    (duct - duct_hole).extrude(-DUCT_DEPTH);
}

Bottom();
TopDuct();
Cover();
